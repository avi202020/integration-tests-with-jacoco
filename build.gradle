plugins {
    id 'java'
    id 'jacoco'
    id 'org.sonarqube' version '3.3'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation("org.hamcrest:hamcrest-library:2.2")
}

testing {
    suites {
        test {
            useJUnitJupiter()
        }
        integrationTest(JvmTestSuite) {
            dependencies {
                implementation project
            }
        }
    }
}

tasks.named('check').configure {
    dependsOn tasks.named('integrationTest')
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

jacocoTestReport {
    dependsOn test
}

sonarqube {
    properties {
        property 'sonar.host.url', 'http://localhost:9000'
        property 'sonar.coverage.jacoco.xmlReportPaths', "$buildDir/reports/jacoco/test/jacocoTestReport.xml,$buildDir/reports/jacoco/jacocoIntegrationTestReport/jacocoIntegrationTestReport.xml"
    }
}

tasks.register('jacocoIntegrationTestReport', JacocoReport) {
    reports {
        xml.required = true
    }
    sourceSets sourceSets.main
    executionData tasks.integrationTest
    dependsOn tasks.integrationTest
}

tasks.named('sonarqube').configure {
    dependsOn jacocoTestReport, jacocoIntegrationTestReport
}